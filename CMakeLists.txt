CMAKE_MINIMUM_REQUIRED(VERSION 3.17)

SET(CMAKE_VERBOSE_MAKEFILE ON)
# Project properties
SET(PROJECT_ORG pyrene-dev)
SET(PROJECT_NAME talos_data)
SET(PROJECT_DESCRIPTION "Talos urdf model, srdf model and other data")
SET(PROJECT_URL "https://gepgitlab.laas.fr/${PROJECT_ORG}/${PROJECT_NAME}")

# Project options
OPTION(SUFFIX_SO_VERSION "Suffix library name with its version" ON)

# Project configuration
SET(PROJECT_USE_CMAKE_EXPORT TRUE)
SET(CXX_DISABLE_WERROR TRUE)
SET(CMAKE_CXX_STANDARD 11)

# JRL-cmakemodule setup
INCLUDE(cmake/base.cmake)

# Project definition
COMPUTE_PROJECT_ARGS(PROJECT_ARGS LANGUAGES CXX C)
PROJECT(${PROJECT_NAME} ${PROJECT_ARGS})

# Project dependencies
ADD_PROJECT_DEPENDENCY(urdfdom REQUIRED)

# TEST IF ROS IS PRESENT AND SETUP
IF(DEFINED ENV{ROS_VERSION} )

  # IF ROS1
  IF ($ENV{ROS_VERSION} EQUAL 1)
    # Dependency to roscpp is also optional
    ADD_PROJECT_DEPENDENCY(roscpp)
    find_package(catkin REQUIRED COMPONENTS roscpp)
  ENDIF()

  # IF ROS2
  IF ($ENV{ROS_VERSION} EQUAL 2)
    # Dependency to roscpp is also optional
    ADD_PROJECT_DEPENDENCY(ros)
    find_package(ament_cmake REQUIRED)
    find_package(rclcpp REQUIRED)
  ENDIF()

ENDIF(DEFINED ENV{ROS_VERSION})

ADD_PROJECT_DEPENDENCY(GAZEBO OPTIONAL)

# If Gazebo is found then compile the spring plug-in.
IF(GAZEBO)

  ADD_LIBRARY(${PROJECT_NAME} SHARED
    src/SpringPlugin.cc)
  TARGET_INCLUDE_DIRECTORIES(${PROJECT_NAME}
    PRIVATE ${GAZEBO_INCLUDE_DIRS} ${catkin_INCLUDE_DIRS})

  IF(SUFFIX_SO_VERSION)
    SET_TARGET_PROPERTIES(${PROJECT_NAME}
      PROPERTIES SOVERSION ${PROJECT_VERSION})
  ENDIF(SUFFIX_SO_VERSION)

  INSTALL(TARGETS ${PROJECT_NAME} EXPORT ${TARGETS_EXPORT_NAME} DESTINATION lib)

ENDIF(GAZEBO)


FOREACH(dir config gazebo meshes robots urdf srdf launch)
  INSTALL(DIRECTORY ${dir} DESTINATION share/${PROJECT_NAME})
ENDFOREACH(dir)

INSTALL(FILES package.xml DESTINATION share/${PROJECT_NAME})
SET(PACKAGE_EXTRA_MACROS "set(TALOS_DATA_PREFIX ${CMAKE_INSTALL_PREFIX})")
